/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Providers;

use Runtime.BaseProvider;
use Runtime.Component;
use Runtime.VirtualDom;


class RenderContent extends BaseProvider
{
	Map components = {};
	
	
	/**
	 * Add replace component
	 */
	void addComponent(string component, string name)
	{
		this.components.set(component, name);
	}
	
	
	/**
	 * Find component
	 */
	string findComponent(VirtualDom vdom)
	{
		string name = vdom.name;
		if (this.components.has(name)) name = this.compoentns.get(name);
		return name;
	}
	
	
	/**
	 * Render
	 */
	void render(var vdom, Collection content, VirtualDom parent_vdom = null)
	{
		if (not vdom) return;
		if (not(vdom instanceof VirtualDom))
		{
			if (parent_vdom and parent_vdom.is_raw)
			{
				content.push(vdom);
			}
			else content.push(rs::escapeHtml(vdom));
			return;
		}
		
		if (vdom.is_component)
		{
			string component_name = this.findComponent(vdom);
			Component component = rtl::newInstance(component_name);
			component.layout = vdom.component ? vdom.component.layout : null;
			component._slots = vdom.slots;
			Collection<string> keys = rtl::list(vdom.attrs.keys());
			for (int i=0; i<keys.count(); i++)
			{
				string attr_name = keys.get(i);
				rtl::setAttr(component, attr_name, vdom.attrs.get(attr_name));
			}
			VirtualDom item = component.render();
			this.render(item, content);
		}
		else
		{
			Vector item_result = [];
			if (not vdom.attrs.has("@raw"))
			{
				for (int i=0; i<vdom.items.count(); i++)
				{
					VirtualDom item = vdom.items.get(i);
					this.render(item, item_result, vdom);
				}
			}
			else
			{
				item_result.push(vdom.attrs.get("@raw"));
			}
			if (vdom.name != "")
			{
				Vector attr_items = [];
				if (vdom.attrs)
				{
					Collection<string> keys = rtl::list(vdom.attrs.keys());
					for (int i=0; i<keys.count(); i++)
					{
						string attr_name = keys.get(i);
						string value = rs::trim(vdom.attrs.get(attr_name));
						if (value == "") continue;
						if (rs::charAt(attr_name, 0) == "@") continue;
						attr_items.push(attr_name ~ "=\"" ~ rs::escapeHtml(value) ~ "\"");
					}
				}
				string attrs = attr_items.count() > 0 ? " " ~ rs::join(" ", attr_items) : "";
				if (vdom.name == "br" or vdom.name == "hr" or vdom.name == "link")
				{
					content.push("<" ~ vdom.name ~ attrs ~ "/>");
				}
				else
				{
					content.push("<" ~ vdom.name ~ attrs ~ ">");
					content.appendItems(item_result);
					content.push("</" ~ vdom.name ~ ">");
				}
			}
			else
			{
				bool is_fragment = vdom.is_render and vdom.items.count() > 1;
				if (is_fragment) content.push("<!--[-->");
				content.appendItems(item_result);
				if (is_fragment) content.push("<!--]-->");
			}
		}
	}
}