/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Providers;

use Runtime.lib;
use Runtime.BaseHook;
use Runtime.BaseProvider;
use Runtime.Callback;
use Runtime.Chain;
use Runtime.ChainAsync;
use Runtime.Context;
use Runtime.Entity.Hook;


class HookProvider extends BaseProvider
{
	protected Vector<BaseHook> base_hooks = [];
	protected Map<Chain> chains = {};
	protected Map<bool> async_hooks = {};
	
	
	/**
	 * Init provider
	 */
	async void init()
	{
		Vector<Hook> hooks = @.getEntities(classof Hook);
		
		this.base_hooks = new Vector();
		for (int i=0; i<hooks.count(); i++)
		{
			Hook hook = hooks[i];
			BaseHook base_hook = hook.createInstance({
				"hook": hook,
				"provider": this,
			});
			base_hook.register_hooks();
			this.base_hooks.push(base_hook);
		}
	}
	
	
	/**
	 * Start provider
	 */
	async void start()
	{
		await parent::start();
	}
	
	
	/**
	 * Set async
	 */
	void setAsync(Vector arr)
	{
		for (int i=0; i<arr.count(); i++)
		{
			string hook_name = arr.get(i);
			this.async_hooks.set(hook_name, true);
		}
	}
	
	
	/**
	 * Returns if chain is async
	 */
	bool isAsync(string name)
	{
		if (not this.async_hooks.has(name)) return false;
		return this.async_hooks.get(name);
	}
	
	
	/**
	 * Create chain
	 */
	void createChain(string name, bool is_async)
	{
		if (this.chains.has(name)) return;
		if (is_async) this.chains.set(name, new ChainAsync());
		else this.chains.set(name, new Chain());
	}
	
	
	/**
	 * Returns chain
	 */
	Chain getChain(string name)
	{
		if (not this.chains.has(name)) this.createChain(name, this.isAsync(name));
		return this.chains.get(name);
	}
	
	
	/**
	 * Apply hook
	 */
	void apply(string hook_name, Map params = null)
	{
		Chain chain = this.chains.get(hook_name);
		if (not chain) return params;
		
		if (chain instanceof ChainAsync)
		{
			fn f = async void () use (chain, params)
			{
				await chain.apply([params]);
				return params;
			}
			return f();
		}
		
		chain.apply([params]);
		return params;
	}
}