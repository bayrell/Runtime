/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Serializer;

use Runtime.BaseObject;
use Runtime.Serializer.BaseType;
use Runtime.Serializer.TypeError;


class VectorType extends BaseObject implements BaseType
{
	BaseType field = null;
	
	
	/**
	 * Create object
	 */
	void constructor(BaseType field)
	{
		parent();
		this.field = field;
	}
	
	
	/**
	 * Filter value
	 */
	Vector filter(var value, Vector errors, var old_value)
	{
		if (value === null) return null;
		if (not(value instanceof Vector)) return null;
		Vector new_value = [];
		for (int i=0; i<value.count(); i++)
		{
			var item = value.get(i);
			var new_item = this.field.filter(item, errors);
			new_value.push(new_item);
			TypeError::addFieldErrors(errors, "[" ~ i ~ "]");
		}
		return new_value;
	}
	
	
	/**
	 * Returns data
	 */
	Vector serialize(Vector value)
	{
		if (value === null) return null;
		if (not(value instanceof Vector)) return null;
		Vector new_value = [];
		for (int i=0; i<value.count(); i++)
		{
			var item = value.get(i);
			var new_item = this.field.serialize(item);
			new_value.push(new_item);
		}
		return new_value;
	}
}