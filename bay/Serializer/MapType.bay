/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Serializer;

use Runtime.BaseObject;
use Runtime.SerializeInterface;
use Runtime.Serializer.BaseType;
use Runtime.Serializer.TypeError;


class MapType extends BaseObject implements BaseType
{
	Vector<BaseType> fields = null;
	Map<Vector<BaseType>> items = {};
	
	
	/**
	 * Fields
	 */
	void constructor(var params = null)
	{
		parent();
		if (params implements BaseType) this.fields = [params];
		else if (params instanceof Vector) this.fields = params;
		else if (params instanceof Map) this.items = params;
	}
	
	
	/**
	 * Add type
	 */
	void addType(string key, BaseType rule)
	{
		if (not this.items.has(key)) this.items.set(key, []);
		Vector items = this.items.get(key);
		items.push(rule);
	}
	
	
	/**
	 * Returns rules
	 */
	Vector<BaseType> getRules(string field_name)
	{
		if (this.items and this.items.has(field_name))
		{
			Vector fields = this.items.get(field_name);
			if (not (fields instanceof Vector)) fields = [fields];
			return fields;
		}
		if (this.fields) return this.fields;
		return null;
	}
	
	
	/**
	 * Returns keys
	 */
	Vector keys(Map value = null) =>
		rtl::list((this.fields and value) ? value.keys() : this.items.keys())
	;
	
	
	/**
	 * Walk fields
	 */
	void walk(Map value, Vector errors, fn f)
	{
		Map new_value = {};
		
		Vector keys = this.keys(value);
		for (int i=0; i<keys.count(); i++)
		{
			string key = keys.get(i);
			var item_value = null;
			if (value implements SerializeInterface) item_value = rtl::attr(value, key);
			else if (value instanceof Map) item_value = value.get(key);
			
			Vector fields = this.getRules(key);
			Vector item_errors = [];
			if (fields)
			{
				for (int j=0; j<fields.count(); j++)
				{
					BaseType item = fields.get(j);
					if (item implements BaseType)
					{
						item_value = f(item, item_value, item_errors, key);
					}
				}
			}
			new_value.set(key, item_value);
			TypeError::addFieldErrors(item_errors, key);
			errors.appendItems(item_errors);
		}
		
		return new_value;
	}
	
	
	/**
	 * Filter type
	 */
	Map filter(Map value, Vector errors, Map old_value = null, SerializeInterface prev = null)
	{
		if (not (value instanceof Map)) return null;
		
		Map new_value = this.walk(
			value, errors,
			void (BaseType rule, var item_value, Vector item_errors, string key)
				use (old_value, prev)
			{
				return rule.filter(
					item_value, item_errors,
					old_value ? old_value.get(key) : null, prev
				);
			}
		);
		
		return new_value;
	}
	
	
	/**
	 * Serialize
	 */
	Map encode(Map value)
	{
		if (not (value instanceof Map)) return null;
		
		Vector errors = [];
		Map new_value = this.walk(
			value, errors,
			void (BaseType rule, var item_value, Vector item_errors)
			{
				return rule.encode(item_value);
			}
		);
		
		return new_value;
	}
}