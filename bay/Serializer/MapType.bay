/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Serializer;

use Runtime.BaseObject;
use Runtime.Serializer.BaseType;
use Runtime.Serializer.TypeError;


class MapType extends BaseObject implements BaseType
{
	Vector<BaseType> fields = [];
	
	
	/**
	 * Fields
	 */
	void constructor(BaseType fields = null)
	{
		parent();
		if (fields implements BaseType) this.fields = [fields];
		else if (fields instanceof Vector) this.fields = fields;
	}
	
	
	/**
	 * Filter type
	 */
	Map filter(Map value, Vector errors, Map old_value)
	{
		if (not (value instanceof Map)) return null;
		Map new_value = {};
		Vector keys = rtl::list(value.keys());
		for (int i=0; i<keys.count(); i++)
		{
			string key = keys.get(i);
			var item_value = value.get(key);
			Vector item_errors = [];
			for (int j=0; j<this.fields.count(); j++)
			{
				BaseType item = this.fields.get(j);
				if (item implements BaseType)
				{
					item_value = item.filter(
						item_value, item_errors,
						old_value ? old_value.get(key) : null
					);
				}
			}
			new_value.set(key, item_value);
			errors.appendItems(item_errors);
			TypeError::addFieldErrors(item_errors, key);
		}
		return new_value;
	}
	
	
	/**
	 * Serialize
	 */
	Map serialize(Map value)
	{
		if (not (value instanceof Map)) return null;
		Map new_value = {};
		Vector keys = rtl::list(value.keys());
		for (int i=0; i<keys.count(); i++)
		{
			string key = keys.get(i);
			var item_value = value.get(key);
			for (int j=0; j<this.fields.count(); j++)
			{
				BaseType item = this.fields.get(j);
				if (item implements BaseType)
				{
					item_value = item.serialize(item_value);
				}
			}
			new_value.set(key, item_value);
		}
		return new_value;
	}
}