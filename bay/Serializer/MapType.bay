/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Serializer;

use Runtime.BaseObject;
use Runtime.Serializer.BaseType;
use Runtime.Serializer.TypeError;


class MapType extends BaseObject implements BaseType
{
	Vector<BaseType> fields = null;
	Map<BaseType> params = null;
	
	
	/**
	 * Fields
	 */
	void constructor(var params = null)
	{
		parent();
		if (params implements BaseType) this.fields = [params];
		else if (params instanceof Vector) this.fields = params;
		else if (params instanceof Map)
		{
			this.params = params;
		}
	}
	
	
	/**
	 * Returns rules
	 */
	Vector<BaseType> getRules(string field_name)
	{
		if (this.fields) return this.fields;
		if (this.params and this.params.has(field_name))
		{
			Vector fields = this.params.get(field_name);
			if (not (fields instanceof Vector)) fields = [fields];
			return fields;
		}
		return null;
	}
	
	
	/**
	 * Walk fields
	 */
	void walk(Map value, Vector errors, Map old_value, fn f)
	{
		Map new_value = {};
		Vector keys = [];
		if (this.params) keys = rtl::list(this.params.keys());
		else keys = rtl::list(value.keys());
		
		for (int i=0; i<keys.count(); i++)
		{
			string key = keys.get(i);
			var item_value = value.get(key);
			bool setup = false;
			
			Vector fields = this.getRules(key);
			if (fields == null and this.params == null) setup = true;
			
			Vector item_errors = [];
			if (fields)
			{
				for (int j=0; j<fields.count(); j++)
				{
					BaseType item = fields.get(j);
					if (item implements BaseType)
					{
						item_value = f(
							item, item_value, item_errors,
							old_value ? old_value.get(key) : null
						);
					}
				}
				setup = true;
			}
			
			if (setup) new_value.set(key, item_value);
			
			errors.appendItems(item_errors);
			TypeError::addFieldErrors(item_errors, key);
		}
		
		return new_value;
	}
	
	
	/**
	 * Filter type
	 */
	Map filter(Map value, Vector errors, Map old_value)
	{
		if (not (value instanceof Map)) return null;
		
		Map new_value = this.walk(
			value, errors, old_value,
			void (BaseType rule, var item_value, Vector item_errors, var old_item_value)
			{
				return rule.filter(
					item_value, item_errors,
					old_item_value
				);
			}
		);
		
		return new_value;
	}
	
	
	/**
	 * Serialize
	 */
	Map serialize(Map value)
	{
		if (not (value instanceof Map)) return null;
		
		Vector errors = [];
		Map new_value = this.walk(
			value, errors, null,
			void (BaseType rule, var item_value, Vector item_errors, var old_item_value)
			{
				return rule.serialize(item_value);
			}
		);
		
		return new_value;
	}
}