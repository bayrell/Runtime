/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Serializer;

use Runtime.BaseObject;


class TypeError extends BaseObject
{
	Vector<string> path = [];
	string message = "";
	
	
	/**
	 * Create type error
	 */
	void constructor(string message)
	{
		parent();
		this.message = message;
	}
	
	
	/**
	 * Returns error message
	 */
	string getMessage()
	{
		Vector path = this.path.slice().reverse();
		string field_name = path.first();
		if (path.count() > 1) field_name ~= "[" ~ rs::join("][", path) ~ "]";
		return field_name ~ ": " ~ this.message;
	}
	
	
	/**
	 * Returns messages;
	 */
	static Vector getMessages(Vector<TypeError> errors) =>
		errors.map(string (TypeError error) => error.getMessage())
	;
	
	
	/**
	 * Add key to field name
	 */
	void addKey(string key)
	{
		this.path.push(key);
	}
	
	
	/**
	 * Add field to errors
	 */
	static void addFieldErrors(Vector<TypeError> errors, string key)
	{
		for (int i=0; i<errors.count(); i++)
		{
			TypeError item = errors.get(i);
			if (rtl::isString(item))
			{
				item = new TypeError(item);
				errors.set(i, item);
			}
			item.addKey(key);
		}
	}
}