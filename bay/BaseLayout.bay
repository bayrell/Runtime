/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime;

use Runtime.BaseModel;
use Runtime.BaseStorage;
use Runtime.DefaultLayout;
use Runtime.Method;
use Runtime.Hooks.RuntimeHook;
use Runtime.Serializer.MapType;
use Runtime.Serializer.ObjectType;
use Runtime.Serializer.Serializer;
use Runtime.Serializer.StringType;
use Runtime.Serializer.VectorType;


class BaseLayout extends BaseModel
{
	BaseStorage storage = null;
	Vector<string> components = [];
	Map pages = {};
	string component = classof DefaultLayout;
	string current_page_model = "";
	string name = "";
	string lang = "en";
	string title = "";
	string theme = "light";
	
	
	/**
	 * Init params
	 */
	void initParams(Dict params)
	{
		parent(params);
		this.layout = this;
	}
	
	
	/**
	 * Init widget settings
	 */
	void initWidget(Dict params)
	{
		parent(params);
		
		/* Init storage */
		this.initStorage();
	}
	
	
	/**
	 * Init storage
	 */
	void initStorage()
	{
		this.storage = this.createWidget(classof BaseStorage);
	}
	
	
	/**
	 * Serialize object
	 */
	void serialize(Serializer serializer)
	{
		parent(serializer);
		serializer.addType("components", new VectorType(new StringType()));
		serializer.addType("current_page_model", new StringType());
		serializer.addType("lang", new StringType());
		serializer.addType("theme", new StringType());
		serializer.addType("title", new StringType());
		serializer.addType("pages", new MapType(new ObjectType{
			"extends": classof BaseModel,
			"create": BaseModel (Map data)
			{
				string class_name = data.get("__class_name__");
				if (not rtl::classExists(class_name)) return null;
				if (not rtl::isInstanceOf(class_name, classof BaseModel)) return null;
				return this.createWidget(class_name, data);
			},
			"serialize": Map (Map data, BaseModel model)
			{
				data.set("__class_name__", model::getClassName());
			}
		}));
		serializer.addType("storage", new ObjectType({"class_name": classof BaseStorage}));
	}
	
	
	/**
	 * Add component
	 */
	void addComponent(string class_name)
	{
		this.components.push(class_name);
	}
	
	
	/**
	 * Returns page model
	 */
	BaseModel getPageModel() => this.pages.get(this.current_page_model);
	
	
	/**
	 * Set page model
	 */
	BaseModel setPageModel(string class_name, Map params=null)
	{
		if (not params) params = {};
		this.current_page_model = class_name;
		
		BaseModel page = this.pages.get(class_name);
		if (not page)
		{
			params.set("widget_name", class_name);
			page = this.createWidget(class_name, params);
			this.pages.set(class_name, page);
		}
		
		return page;
	}
	
	
	/**
	 * Set page title
	 */
	void setPageTitle(string title, bool full_title=false)
	{
		Map res = @.hook(RuntimeHook::TITLE, {
			"layout": this,
			"title": title,
			"title_orig": title,
			"full_title": full_title,
		});
		this.title = res.get("title");
	}
	
	
	/**
	 * Returns object
	 */
	var get(string name) => this.storage.frontend_params.get(name);
	
	
	/**
	 * Translate
	 */
	string translate(Map text, Map params = null)
	{
		string s = text.has(this.lang) ? text.get(this.lang) :
			text.get(this.getDefaultLang())
		;
		return rs::format(s, params);
	}
	
	
	/**
	 * Returns default lang
	 */
	string getDefaultLang() => "en";
	
	
	/**
	 * Assets
	 */
	void assets(string path)
	{
		Map res = @.hook(RuntimeHook::ASSETS, {
			"layout": this,
			"path": path,
		});
		return res.get("path");
	}
	
	
	/**
	 * Returns required components
	 */
	static void getRequiredComponents(string component, Vector result, Map hash)
	{
		if (hash.has(component)) return;
		hash.set(component, true);
		Method f = new Method(component, "getRequiredComponents");
		if (f.exists())
		{
			Vector items = f.apply();
			for (int i=0; i<items.count(); i++)
			{
				string name = items.get(i);
				if (not hash.has(name))
				{
					static::getRequiredComponents(name, result, hash);
				}
			}
		}
		result.push(component);
	}
	
	
	/**
	 * Returns all components
	 */
	Vector getComponents()
	{
		Map hash = {};
		Map res = @.hook(RuntimeHook::COMPONENTS, {
			"components": this.components.slice(),
		});
		Vector result_components = [];
		Vector components = res.get("components");
		for (int i=0; i<components.count(); i++)
		{
			string class_name = components.get(i);
			static::getRequiredComponents(class_name, result_components, hash);
		}
		return result_components;
	}
	
	
	/**
	 * Returns style
	 */
	string getStyle()
	{
		Vector content = [];
		Vector components = this.getComponents();
		for (int i=0; i<components.count(); i++)
		{
			string class_name = components.get(i);
			Method f = new Method(class_name, "getComponentStyle");
			if (not f.exists()) continue;
			content.push(f.apply());
		}
		return rs::join("", content);
	}
}