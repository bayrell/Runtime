/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime;

use Runtime.BaseObject;
use Runtime.BaseModel;
use Runtime.Component;
use Runtime.Serializer;
use Runtime.VirtualDom;
use Runtime.Hooks.RuntimeHook;
use Runtime.Providers.RenderContent;


class RenderContainer extends BaseObject
{
	BaseModel layout = null;
	
	
	/**
	 * Create layout
	 */
	BaseModel createLayout(string layout_name)
	{
		string class_name = "Runtime.BaseLayout";
		
		/* Get layout params */
		Dict params = @.hook(RuntimeHook::LAYOUT_NAME, {
			"class_name": class_name,
			"layout_name": layout_name,
		});
		
		this.layout = rtl::newInstance(params.get("class_name"), [params]);
		this.layout.name = layout_name;
		
		/* Call create layout */
		@.hook(RuntimeHook::CREATE_LAYOUT, {
			"container": this,
		});
		
		return this.layout;
	}
	
	
	/**
	 * Change layout
	 */
	void changeLayout(string layout_name)
	{
		if (this.layout and this.layout.name == layout_name) return;
		
		/* Save widgets */
		Map old_widgets = this.layout ? this.layout.widgets : {};
		
		/* Create new layout */
		this.createLayout(layout_name);
		
		/* Restore widgets */
		Vector keys = rtl::list(old_widgets.keys());
		for (int i=0; i<keys.count(); i++)
		{
			string key = keys.get(i);
			BaseModel widget = old_widgets.get(key);
			widget.parent_widget = this.layout;
			this.layout.widgets.set(key, widget);
		}
		
		/* Restore storage */
		if (old_widgets.has("storage")) this.layout.storage = old_widgets.get("storage");
		
		/* Call create layout */
		@.hook(RuntimeHook::CHANGE_LAYOUT, {
			"container": this,
		});
	}
	
	
	/**
	 * Resolve container
	 */
	async void resolve()
	{
	}
	
	
	/**
	 * Render page model
	 */
	async void renderPageModel(string model_name, Dict params = null)
	{
		/* Set page model */
		this.layout.setPageModel(model_name, params);
		
		/* Action index */
		BaseModel page_model = this.layout.getPageModel();
		if (page_model)
		{
			await page_model.loadData(this);
			page_model.buildTitle(this);
		}
	}
	
	
	/**
	 * Load data
	 */
	async void loadData()
	{
		await this.layout.loadData();
	}
	
	
	/**
	 * Returns data
	 */
	Dict getData()
	{
		Map layout_data = rtl::serialize(this.layout);
		Map data = {
			"modules": @.modules,
			"class": this.layout::getClassName(),
			"layout": layout_data,
			"environments": {
				"CLOUD_ENV": @.env("CLOUD_ENV"),
				"DEBUG": @.env("DEBUG"),
				"LOCALE": @.env("LOCALE"),
				"TZ": @.env("TZ"),
				"TZ_OFFSET": @.env("TZ_OFFSET"),
			},
		};
		Map res = @.hook(RuntimeHook::CREATE_CONTAINER_DATA, {
			"container": this,
			"data": data,
		});
		return res.get("data");
	}
	
	
	/**
	 * Render app
	 */
	string renderApp()
	{
		Component component = rtl::newInstance(this.layout.component);
		component.container = this;
		component.layout = this.layout;
		VirtualDom vdom = component.renderApp();
		return vdom.render();
	}
	
	
	/**
	 * Render layout
	 */
	string render()
	{
		VirtualDom vdom = new VirtualDom();
		vdom.setName(this.layout.component);
		vdom.setAttrs({"layout": this.layout});
		return vdom.render();
	}
}