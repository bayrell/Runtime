/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2018 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.bayrell.org/licenses/APACHE-LICENSE-2.0.html
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime;

use Runtime.Context;
use Runtime.Map;
use Runtime.rs;
use Runtime.rtl;
use Runtime.Vector;
use Runtime.Interfaces.ContextInterface;
use Runtime.Interfaces.FactoryInterface;
use Runtime.Interfaces.SerializeInterface;


#switch
#case ifcode JAVASCRIPT then
var isBrowser=function(){return typeof window !== "undefined" && this === window;}
#endswitch


export class Utils{
	
	
	protected static var _global_context = null;
	
	
	/**
	 * Returns global context
	 * @return ContextInterface
	 */
	public static ContextInterface globalContext(){
		#switch
		#case ifcode PHP then
		return self::$_global_context;
		#case ifcode JAVASCRIPT then
		if (isBrowser()) return Runtime.Utils._global_context;
		return Utils._global_context;
		#endswitch
	}
	
	
	
	/**
	 * Set global context
	 * @param ContextInterface context
	 */
	public static ContextInterface setGlobalContext(ContextInterface context){
		#switch
		#case ifcode PHP then
		self::$_global_context = $context;
		#case ifcode JAVASCRIPT then
		if (isBrowser()) Runtime.Utils._global_context = context;
		else Utils._global_context = context;
		#endswitch
		return context;
	}
	
	
	
	/**
	 * Returns global context
	 * @param Context context
	 */
	public static ContextInterface getGlobalContext(){
		return self::globalContext();
	}
	
	
	/**
	 * Register global Context
	 */
	public static ContextInterface createContext(Vector<string> modules = null){
		
		ContextInterface context = new Context();
		if (modules != null){
			modules.each(void (string module) use (context){
				context.registerModule(module);
			});
		}
		
		return context;
	}
	
	
	
	/**
	 * Register global Context
	 */
	public static ContextInterface registerGlobalContext(Vector<string> modules = null){
		ContextInterface context = self::createContext(modules);
		context.init();
		self::setGlobalContext(context);
		return context;
	}
	
	
	
	/**
	 * Returns parents class names
	 * @return Vector<string>
	 */
	public static Vector<string> getParents(string class_name){
		Vector<string> res = new Vector();
		
		while (class_name != ""){
			class_name = rtl::callStaticMethod(class_name, "getParentClassName");
			if (class_name != ""){
				res.push(class_name);
			}
		}
		
		return res;
	}
	
	
	
	/**
	 * Returns true if class exists
	 * @return Vector<string>
	 */
	declare public static Vector<string> getInterfaces(string class_name);
	
	#switch
	#case ifcode PHP then
	static function getInterfaces($class_name){
		$arr = array_values(class_implements(rtl::find_class($class_name)));
		$v = (new Vector())->_assignArr($arr);
		$v = $v->map(function ($s){
			return str_replace("\\", ".", $s);
		});
		return $v;
	}
	#case ifcode JAVASCRIPT then
	static getInterfaces(class_name){
		return new Vector();
	}
	#endswitch
	
	
	
	/**
	 * Returns true if value is primitive value
	 * @return boolean 
	 */
	public static bool isPrimitiveValue(var value){
		if (rtl::isScalarValue(value)) return true;
		if (value instanceof Vector) return true;
		if (value instanceof Map) return true;
		return false;
	}
	
	
	
	/**
	 * Get value from object
	 */
	static mixed get(mixed obj, string key, mixed default_value = null){
		if (obj instanceof Vector){
			return obj.get(key, default_value);
		}
		if (obj instanceof Map){
			return obj.get(key, default_value);
		}
		
		#switch
		#case ifcode JAVASCRIPT then
		if (typeof obj == 'object'){
			if (typeof obj[key] != undefined) 
				return obj[key];
		}
		#endswitch
		
		return default_value;
	}
	
	
	
	/**
	 * Set value to object
	 */
	static mixed set(mixed obj, string key, mixed value = null){
		if (obj instanceof Vector){
			obj.set(key, value);
		}
		if (obj instanceof Map){
			obj.set(key, value);
		}
		
		#switch
		#case ifcode JAVASCRIPT then
		if (typeof obj == 'object'){
			obj[key] = value;
		}
		#endswitch
	}
	
	
	
	/**
	 * Call each
	 */
	static mixed each(mixed obj, func f){
		if (obj instanceof Vector){
			obj.each(f);
		}
		if (obj instanceof Map){
			obj.each(f);
		}
	}
	
	
	/**
	 * Convert bytes to string
	 * @param Vector<byte> arr - vector of the bytes
	 * @string charset - charset of the bytes vector. Default utf8
	 * @return string
	 */
	public string bytesToString(Vector<byte> arr, string charset="utf8"){}
	
	
	
	/**
	 * Convert string to bytes
	 * @param string s - incoming string
	 * @param Vector<byte> arr - output vector
	 * @param charset - Result bytes charset. Default utf8
	 */
	public void stringToBytes(string s, Vector<byte> arr, string charset="utf8"){}
	
	
	
	/**
	 * Translate message
	 * @params string message - message need to be translated
	 * @params MapInterface params - Messages params. Default null.
	 * @params string locale - Different locale. Default "".
	 * @return string - translated string
	 */
	public static string translate(string message, MapInterface params = null, string locale = "", 
	Object context = null){
			
		if (context == null) context = self::globalContext();
		if (context != null) {
			Vector<var> args = (new Vector<var>())
				.push(message)
				.push(params)
				.push(locale)
			;
			return rtl::callMethod(
				context,
				"translate",
				args
			);
		}	
		return message;
	}
	
	
	
	/**
	 * Compare 2 Vectors, Returns true if arr1 and arr2 have same class names
	 * @param Vector<string> arr1
	 * @param Vector<string> arr2
	 * @return bool
	 */
	public static bool equalsVectors(Vector<string> arr1, Vector<string> arr2){
		for (int i=0; i<arr1.count(); i++){
			string item = arr1.item(i);
			if (arr2.indexOf(item) == -1)
				return false;
		}
		for (int i=0; i<arr2.count(); i++){
			string item = arr2.item(i);
			if (arr1.indexOf(item) == -1)
				return false;
		}
		return true;
	}
	
	
	
	/**
	 * Returns object to primitive value
	 * @param mixed obj
	 * @return mixed
	 */
	public static mixed ObjectToPrimitive(mixed obj){
	
		if (obj === null) return null;
		if (rtl::isScalarValue(obj)) return obj;
		if (obj instanceof Vector){
			return obj.map(mixed (mixed value){
				return self::ObjectToPrimitive( value );
			});
		}
		if (obj instanceof Map){
			obj = obj.map(mixed (string key, mixed value){
				return self::ObjectToPrimitive( value );
			});
			return obj;
		}
		if (obj implements SerializeInterface){
			
			Vector<string> names = new Vector();
			Map<string, mixed> values = new Map();
			obj.getVariablesNames(names);
			
			names.each(void (string variable_name) use (values, obj){
				var value = obj.takeValue(variable_name, null);
				var value = self::ObjectToPrimitive( value );
				values.set(variable_name, value);
			});
			
			values.set("__class_name__", obj.getClassName());
			
			return values;
		}
		
		return null;
	}
	
	
	
	/**
	 * Returns object to primitive value
	 * @param SerializeContainer container
	 * @return mixed
	 */
	public static mixed PrimitiveToObject(mixed obj, ContextInterface context = null){
		
		if (obj === null) return null;
		if (rtl::isScalarValue(obj)) return obj;
		if (context == null) context = self::globalContext();
		
		if (obj instanceof Vector){
			return obj.map(mixed (mixed value) use (context){
				return self::PrimitiveToObject( value, context );
			});
		}
		
		if (obj instanceof Map){
			
			obj = obj.map(mixed (string key, mixed value) use (context){
				return self::PrimitiveToObject( value, context );
			});
			
			if ( !obj.has("__class_name__") ) return obj;
			if ( obj.item("__class_name__") == "Runtime.Map" ){
				obj.remove("__class_name__");
				return obj;
			}
			
			string class_name = obj.item("__class_name__");
			if (not rtl::class_exists(class_name)){
				return null;
			}
			if (not rtl::class_implements(class_name, "Runtime.Interfaces.SerializeInterface")){
				return null;
			}
			
			/* New instance */
			SerializeInterface instance = rtl::newInstance(class_name, null);
			
			/* Assign values */
			Vector<string> names = new Vector();
			instance.getVariablesNames(names);
			names.each(void (string variable_name) use (instance, obj){
				if (variable_name == "__class_name__") 
					return;
				var value = obj.get(variable_name, null);
				instance.assignValue(variable_name, value);
			});
			
			return instance;
		}
		
		return null;
	}
	
	
	
	/**
	 * Json encode serializable values
	 * @param serializable value
	 * @param SerializeContainer container
	 * @return string 
	 */
	declare export static string json_encode(mixed value, bool convert = true);

	#switch
	#case ifcode PHP then
	static function json_encode($value, $convert = true){
		if ($convert){
			$value = self::ObjectToPrimitive($value);
		}
		return json_encode($value, JSON_UNESCAPED_UNICODE);
	}
	#case ifcode JAVASCRIPT then
	static json_encode(value, convert){
		if (convert == undefined) convert = true;
		var _Utils=null;if (isBrowser()) _Utils=Runtime.Utils; else _Utils=Utils;
		var _Vector=null;if (isBrowser()) _Vector=Runtime.Vector; else _Vector=Vector;
		var _Map=null;if (isBrowser()) _Map=Runtime.Map; else _Map=Map;
		var _rtl=null;if (isBrowser()) _rtl=Runtime.rtl; else _rtl=rtl;
		if (convert) value = _Utils.ObjectToPrimitive(value);
		return JSON.stringify(value, function (key, value){
			if (_rtl.isScalarValue(value)) return value;
			if (value instanceof _Vector) return value;
			if (value instanceof _Map) return value.toObject();
			return undefined;
		});
	}
	#endswitch
	
	
	
	/**
	 * Json decode to primitive values
	 * @param string s Encoded string
	 * @return mixed 
	 */
	declare export static mixed json_decode(string s, ContextInterface context = null);

	#switch
	#case ifcode PHP then
	static function json_decode($obj){
		$res = @json_decode($obj, false);
		if (!$res)
			return null;
		return self::NativeToObject($res);
	}
	#case ifcode JAVASCRIPT then
	static json_decode(s, context){
		if (context == undefined) context = null;
		try{
			var _Utils=null;if (isBrowser()) _Utils=Runtime.Utils; else _Utils=Utils;
			var _Vector=null;if (isBrowser()) _Vector=Runtime.Vector; else _Vector=Vector;
			var _Map=null;if (isBrowser()) _Map=Runtime.Map; else _Map=Map;			
			var obj = JSON.parse(s, function (key, value){
				if (Array.isArray(value)){
					return new _Vector(value);
				}
				if (typeof value == 'object'){
					return new _Map(value);
				}
				
				return value;
			});
			return _Utils.PrimitiveToObject(obj,context);
		}
		catch(e){
			return null;
		}
	}
	#endswitch
	
	
	
	#switch
	#case ifcode PHP then
	
	static function NativeToPrimitive($value){
		if ($value === null)
			return null;
			
		if (is_object($value)){
			$res = new \Runtime\Map($value);
			$res = $res->map(function ($key, $val){
				return self::NativeToPrimitive($val);
			});
			return $res;
		}
		
		if (is_array($value)){
			$arr = array_values($value);
			$res = (new \Runtime\Vector())->_assignArr($arr);
			$res = $res->map(function ($item){
				return self::NativeToPrimitive($item);
			});
			return $res;
		}
		
		return $value;
	}
	
	#case ifcode JAVASCRIPT then
	static NativeToPrimitive(value){
		
		var _rtl = null; if (isBrowser()) _rtl=Runtime.rtl; else _rtl=rtl;
		var _Utils = null; if (isBrowser()) _Utils=Runtime.Utils; else _Utils=Utils;
		var _Vector=null; if (isBrowser()) _Vector=Runtime.Vector; else _Vector=Vector;
		var _Map=null; if (isBrowser()) _Map=Runtime.Map; else _Map=Map;
		
		if (value === null)
			return null;
		
		if (Array.isArray(value)){
			var new_value = (new _Vector()).concat(value);
			new_value = new_value.map((val)=>{
				return _Utils.NativeToPrimitive(val);
			});
			return new_value;
		}
		if (typeof value == 'object'){
			var new_value = new _Map(value);
			new_value = new_value.map((key, val)=>{
				return _Utils.NativeToPrimitive(val);
			});
			return new_value;
		}
		
		return value;
	}
	static PrimitiveToNative(value){
		
		var _rtl = null; if (isBrowser()) _rtl=Runtime.rtl; else _rtl=rtl;
		var _Utils = null; if (isBrowser()) _Utils=Runtime.Utils; else _Utils=Utils;
		var _Vector=null; if (isBrowser()) _Vector=Runtime.Vector; else _Vector=Vector;
		var _Map=null; if (isBrowser()) _Map=Runtime.Map; else _Map=Map;
		
		if (value === null)
			return null;
		
		if (value instanceof _Vector){
			var arr = [];
			value.each((v)=>{
				arr.push( _Utils.PrimitiveToNative(v) );
			});
			return arr;
		}
		if (value instanceof _Map){
			var obj = {};
			value.each((k, v)=>{
				obj[k] = _Utils.PrimitiveToNative(v);
			});
			return obj;
		}
		
		return value;
	}
	#endswitch
	
	
	static var ObjectToNative(var value){
		value = self::ObjectToPrimitive(value);
		value = self::PrimitiveToNative(value);
		return value;
	}
	static var NativeToObject(var value){
		value = self::NativeToPrimitive(value);
		value = self::PrimitiveToObject(value);
		return value;
	}

	
	
	/*
	 * Generate password
	 *
	 * @param int length The lenght of the password
	 * @param string options What kinds of the char can be in password
	 *   a - lower case chars
	 *   b - upper case chars
	 *   c - numbers
	 *   d - special chars !@#$%^&?*_-+=~(){}[]<>|/,.:;\\
	 *   e - quotes `"'
	 */	
	public static string randomString(int length=16, string options="abc"){
		string s = "";
		if (rs::strpos(options, "a") >= 0) s ~= "abcdefghjkmnpqrstuvwxyz";
		if (rs::strpos(options, "b") >= 0) s ~= "ABCDEFGHJKMNPQRSTUVWXYZ";
		if (rs::strpos(options, "c") >= 0) s ~= "1234567890";
		if (rs::strpos(options, "d") >= 0) s ~= "!@#$%^&?*_-+=~(){}[]<>|/,.:;\\";
		if (rs::strpos(options, "e") >= 0) s ~= "`\"\'";
		string res = "";
		int c = rs::strlen(s);
		for (int i=0; i<length; i++){
			int k = rtl::random(0, c-1);
			res ~= s[k];
		}
		return res;
	}
}