/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2021 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime;

use Runtime.Collection;
use Runtime.Context;
use Runtime.BaseStruct;
use Runtime.Dict;
use Runtime.Map;
use Runtime.Monad;
use Runtime.rs;
use Runtime.rtl;
use Runtime.Vector;


/* Lambda Functions */
class lib
{
	
	/**
	 * Check object is istance
	 */
	pure fn isInstance(string class_name) =>
		bool (var item) use (class_name) => rtl::is_instanceof(item, class_name)
	;
	
	
	
	/**
	 * Check object is implements interface
	 */	
	pure fn isImplements(string class_name) =>
		bool (var item) use (class_name) => rtl::is_implements(item, class_name)
	;
	
	
	
	/**
	 * Check class is implements interface
	 */	
	pure fn classImplements(string class_name) =>
		bool (string item) use (class_name) => rtl::class_implements(item, class_name)
	;
	
	
	
	/**
	 * Create struct
	 */
	pure fn createStruct(string class_name) =>
		BaseStruct(Dict data) use (class_name) => rtl::newInstance(class_name, [data])
	;
	
	
	
	/**
	 * Equal two struct by key
	 */
	pure fn equal(var value)
		=> bool (var item) use (value)
			=> item == value
	;
	
	
	
	/**
	 * Equal two struct by key
	 */
	pure fn equalNot(var value)
		=> bool (var item) use (value)
			=> item != value
	;
	
	
	
	/**
	 * Equal two struct by key
	 */
	pure fn equalAttr(string key, var value)
		=> bool (BaseStruct item1) use (key, value)
			=> (item1 != null) ? rtl::attr(item1, key) == value : false
	;
	
	
	
	/**
	 * Equal two struct by key
	 */
	pure fn equalNotAttr(string key, var value)
		=> bool (BaseStruct item1) use (key, value)
			=> (item1 != null) ? rtl::attr(item1, key) != value : false
	;
	pure fn equalAttrNot(string key, var value) => static::equalNotAttr(key, value);
	
	
	
	/**
	 * Equal attrs
	 */
	pure fn equalAttrs(Dict search) =>
		bool (Dict item) use (search)
		{
			Collection fields = search.keys();
			for (int i=0; i<fields.count(); i++)
			{
				string field_name = fields[i];
				if (search[field_name] != item[field_name]) return false;
			}
			return true;
		}
	;
	
	
	
	/**
	 * Equal two struct by key
	 */
	pure fn equalMethod(string method_name, var value)
		=> bool (BaseStruct item1) use (method_name, value)
		{
			if (item1 == null) return false;
			fn f = rtl::@method(item1, method_name);
			return f() == value;
		}
	;
	
	
	
	/**
	 * Returns key value of obj
	 */
	pure fn get(string key, var def_value) =>
		var (var obj) use (key, def_value) =>
			rtl::attr(obj, [key], def_value)
	;
	
	
	
	/**
	 * Set value
	 */
	pure fn set(string key, var value) =>
		var (var obj) use (key, value) =>
			rtl::setAttr(obj, [key], value)
	;
	
	
	
	/**
	 * Returns attr of item
	 */
	pure fn attr(Collection<string> path, var def_value = null) =>
		var (var obj) use (path, def_value) =>
			rtl::attr(obj, path, def_value)
	;
	
	
	
	/**
	 * Set dict attr
	 */
	pure fn setAttr(Collection<string> path, var value) =>
		var (var obj) use (path, value) =>
			rtl::setAttr(obj, path, value)
	;
	
	
	
	/**
	 * Returns max id from items
	 */
	pure int getMaxIdFromItems(Collection<BaseStruct> items, int start = 0) =>
		items.reduce
		(
			int (int value, BaseStruct item) => item.id > value ? item.id : value,
			start
		)
	;
	
	
	
	/**
	 * Copy object
	 */
	pure fn copy(Dict d) =>
		bool (var item) use (d) => item.copy(d)
	;
	
	
	
	/**
	 * Take dict
	 */
	pure fn takeDict(Collection<string> fields) =>
		bool (var item) use (fields) => item.takeDict(fields)
	;
	
	
	
	/**
	 * Map
	 */
	pure fn map(fn f) =>
		var (var m) use (f) => m.map(f)
	;
	
	
	
	/**
	 * Filter
	 */
	pure fn filter(fn f) =>
		bool (var m) use (f) => m.filter(f)
	;
	
	
	
	/**
	 * Intersect
	 */
	pure fn intersect(Collection arr) =>
		bool (var m) use (arr) => m.intersect(arr)
	;
	
	
	
	/**
	 * Sort
	 */
	pure fn sort(fn f) =>
		bool (var m) use (f) => m.sortIm(f)
	;
	
	
	
	/**
	 * Transition
	 */
	pure fn transition(fn f) =>
		var (var m) use (f) => m.transition(f)
	;
	
	
	
	/**
	 * Sort asc
	 */
	pure int sortAsc(var a, var b) => (a > b) ? 1 : (a < b) ? -1 : 0;
	
	
	
	/**
	 * Sort desc
	 */
	pure int sortDesc(var a, var b) => (a > b) ? -1 : (a < b) ? 1 : 0;
	
	
	
	/**
	 * Sort attr
	 */
	pure fn sortAttr(string field_name, var f) =>
		int (Dict a, Dict b) use (field_name, f)
		{
			var a = a[field_name];
			var b = b[field_name];
			if (f == "asc") return (a > b) ? 1 : (a < b) ? -1 : 0;
			if (f == "desc") return (a > b) ? -1 : (a < b) ? 1 : 0;
			return f(a, b);
		}
	;
	
	
	
	/**
	 * Convert monad by type
	 */
	pure fn to(string type_value, var def_value = null) =>
		bool (Monad m) use (type_value, def_value) =>
			new Monad
			(
				m.err == null ? rtl::@convert( m.value(), type_value, def_value ) : def_value
			)
	;
	
	
	
	/**
	 * Convert monad by type
	 */
	pure fn default(var def_value = null) =>
		bool (Monad m) use (def_value) =>
			(m.err != null or m.val === null) ? new Monad(def_value) : m
	;
	
	
	
	/**
	 * Set monad new value
	 */
	pure fn newValue(var value = null, bool clear_error = false) =>
		Monad (Monad m) use (value, clear_error) =>
			clear_error == true ? new Monad(value) : m.err == null ? new Monad(value) : m
	;
	
	
	
	/**
	 * Clear error
	 */
	pure fn clearError() =>
		Monad (Monad m) => new Monad(m.val);
	
	
	
	/**
	 * Returns monad
	 */
	pure Monad monad(Monad m) => m;
	
	
	
	/**
	 * Get method from class
	 * @return fn
	 */
	pure fn method(string method_name) =>
		fn (string class_name) use (method_name) =>
			rtl::method(class_name, method_name)
	;
	
	
	
	/**
	 * Apply function
	 * @return fn
	 */
	pure fn applyMethod(string method_name, Collection args = null) =>
		var (string class_name) use (method_name, args)
		{
			fn f = rtl::method(class_name, method_name);
			return rtl::apply(f, args);
		}
	;
	
	
	
	/**
	 * Apply async function
	 * @return fn
	 */
	pure fn applyMethodAsync(string method_name, Collection args = null) =>
		async var (string class_name) use (method_name, args)
		{
			fn f = rtl::method(class_name, method_name);
			return await rtl::applyAsync(f, args);
		}
	;
	
	
	
	/**
	 * Apply function
	 * @return fn
	 */
	pure fn apply(fn f) =>
		var (var value) use (f) =>
			f(value)
	;
	
	
	
	/**
	 * Apply function
	 * @return fn
	 */
	pure fn applyAsync(fn f) =>
		async var (var value) use (f) =>
			await f(value)
	;
	
	
	
	/**
	 * Log message
	 * @return fn
	 */
	pure fn log(string message = "") =>
		var (var value) use (message)
		{
			if (message == "")
			{
				log(value);
			}
			else
			{
				log(message);
			}
			return value;
		}
	;
	
	
	
	/**
	 * Function or
	 */
	pure fn or(Collection<fn> arr) =>
		bool (var item) use (arr)
		{
			for (int i=0; i<arr.count(); i++)
			{
				fn f = arr[i];
				bool res = f(item);
				if (res) return true;
			}
			return false;
		}
	;
	
	
	
	/**
	 * Function and
	 */
	pure fn and(Collection<fn> arr) =>
		bool (var item) use (arr)
		{
			for (int i=0; i<arr.count(); i++)
			{
				fn f = arr[i];
				bool res = f(item);
				if (not res) return false;
			}
			return true;
		}
	;
	
	
	
	/**
	 * Join
	 */
	pure fn join(string ch) => string (Collection items) use (ch) => rs::join(ch, items);
}