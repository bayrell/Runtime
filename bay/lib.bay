/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2020 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime;

use Runtime.Collection;
use Runtime.Context;
use Runtime.CoreStruct;
use Runtime.Dict;
use Runtime.Map;
use Runtime.Monad;
use Runtime.rs;
use Runtime.rtl;
use Runtime.UIStruct;
use Runtime.Vector;


/* Lambda Functions */
class lib
{
	
	/**
	 * Check object is istance
	 */
	pure fn isInstance(string class_name) =>
		bool (var item) use (class_name) => rtl::is_instanceof(item, class_name)
	;
	
	
	
	/**
	 * Check object is implements interface
	 */	
	pure fn isImplements(string class_name) =>
		bool (var item) use (class_name) => rtl::is_implements(item, class_name)
	;
	
	
	
	/**
	 * Check class is implements interface
	 */	
	pure fn classImplements(string class_name) =>
		bool (string item) use (class_name) => rtl::class_implements(item, class_name)
	;
	
	
	
	/**
	 * Create struct
	 */
	pure fn createStruct(string class_name) =>
		CoreStruct(Dict data) use (class_name) => rtl::newInstance(class_name, [data])
	;
	
	
	
	/**
	 * Equal two struct by key
	 */
	pure fn equal(var value)
		=> bool (var item) use (value)
			=> item == value
	;
	
	
	
	/**
	 * Equal two struct by key
	 */
	pure fn equalNot(var value)
		=> bool (var item) use (value)
			=> item != value
	;
	
	
	
	/**
	 * Returns attr of item
	 */
	pure fn attr(Collection<string> path, var def_value = null) =>
		var (var obj) use (path, def_value) =>
			rtl::attr(obj, path, def_value)
	;
	
	
	
	/**
	 * Set dict attr
	 */
	pure fn setAttr(Collection<string> path, var value) =>
		var (var obj) use (path, value) =>
			rtl::setAttr(obj, path, value)
	;
	
	
	
	/**
	 * Equal two struct by key
	 */
	pure fn equalAttr(string key, var value)
		=> bool (CoreStruct item1) use (key, value)
			=> (item1 != null) ? item1.takeValue(key) == value : false
	;
	
	
	
	/**
	 * Equal two struct by key
	 */
	pure fn equalNotAttr(string key, var value)
		=> bool (CoreStruct item1) use (key, value)
			=> (item1 != null) ? item1.takeValue(key) != value : false
	;
	
	
	
	/**
	 * Equal two struct by key
	 */
	pure fn equalMethod(string method_name, var value)
		=> bool (CoreStruct item1) use (method_name, value)
		{
			if (item1 == null) return false;
			fn f = rtl::@method(item1, method_name);
			return f() == value;
		}
	;
	
	
	
	/**
	 * Returns max id from items
	 */
	pure int getMaxIdFromItems(Collection<CoreStruct> items, int start = 0) =>
		items.reduce
		(
			int (int value, CoreStruct item) => item.id > value ? item.id : value,
			start
		)
	;
	
	
	
	/**
	 * Take dict
	 */
	pure fn takeDict(Collection<string> fields) =>
		bool (var item) use (fields) => item.takeDict(fields)
	;
	
	
	
	/**
	 * Filter
	 */
	pure fn filter(fn f) =>
		bool (var m) use (f) => m.filter(f)
	;
	
	
	
	/**
	 * Convert monad by type
	 */
	pure fn to(string type_value, var def_value = null) =>
		bool (var m) use (type_value, def_value) =>
			new Monad
			(
				m.err != null ? rtl::@convert( m.value(), type_value, def_value ) : def_value
			)
	;
	
}