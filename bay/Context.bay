/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2019 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime;

use Runtime.rs;
use Runtime.rtl;
use Runtime.BusResult;
use Runtime.Collection;
use Runtime.CoreDriver;
use Runtime.CoreObject;
use Runtime.CoreStruct;
use Runtime.CoreProvider;
use Runtime.Dict;
use Runtime.Map;
use Runtime.Message;
use Runtime.Vector;
use Runtime.RuntimeConstant;
use Runtime.RuntimeUtils;
use Runtime.Annotations.Driver;
use Runtime.Annotations.Entity;
use Runtime.Annotations.IntrospectionClass;
use Runtime.Annotations.IntrospectionInfo;
use Runtime.Annotations.LambdaChain;
use Runtime.Annotations.Provider;
use Runtime.Exceptions.RuntimeException;
use Runtime.Interfaces.BusInterface;
use Runtime.Interfaces.ModuleDescriptionInterface;


struct Context extends CoreStruct
{
	/* Struct data */
	string base_path = null;
	Dict<string> enviroments = null;
	Dict<var> settings = null;
	Collection<string> modules = null;
	Collection<CoreStruct> entities = null;
	Dict<CoreDriver> drivers = null;
	Dict<CoreStruct> providers = null;
	Dict<var> tags = null;
	
	/* Variable data */
	bool initialized = false;
	bool started = false;
	int start_time = 0;
	
	/* Private data */
	private Vector logs = new Vector();
	
	
	
	/**
	 * Returns app name
	 * @return string
	 */
	pure string appName() => "";
	
	
	
	/**
	 * Returns context settings
	 * @return Dict<string>
	 */
	pure Dict<string> getSettings(Dict env) => null;
	
	
	
	/**
	 * Extends entities
	 */
	pure Collection<CoreStruct> getEntities(Collection<CoreStruct> entities) => null;
	
	
	
	/**
	 * Returns enviroment by eky
	 */
	pure string env(Context c, string key) =>
		(c.enviroments != null) ? c.enviroments.get(key, "") : ""
	;
	
	
	
	/**
	 * Returns settings
	 * @return Dict<string>
	 */
	pure var config(Context c, Collection items, primitive d = null)
	{
		if (c.settings == null) return null;
		Dict<var> config = c.settings.get("config", null);
		return (config != null) ? rtl::attr(config, items, d) : null
	}
	
	
	
	/**
	 * Returns docker secret key
	 */
	pure string secret(Context c, string key)
	{
		Dict<var> secrets = c.settings.get("secrets", null);
		return (secrets != null) ? secrets.get("key", "") : "";
	}
	
	
	
	/**
	 * Create context
	 *
	 * @params Dict env
	 * @params Collection<string> modules
	 * @params Dict settings
	 * @return Context
	 */
	static Context create(Dict env, Collection<CoreStruct> entities = null)
	{
		Dict<var> settings = static::getSettings(env);
		
		/* Context data */
		Dict obj = {
			"enviroments": env,
			"settings": settings,
			"modules": (settings != null) ? settings.get("modules", null) : null,
			"entities": entities,
		};
		
		/* Create context */
		Context ctx = static::newInstance(obj);
		
		return ctx;
	}
	
	
	
	/**
	 * Init context
	 */
	static Context init(Context c)
	{
		if (c.initialized) return c;
		
		/* Extends modules */
		Collection<string> modules = static::getRequiredModules(c.modules);
		
		/* Get modules entities */
		Collection<CoreStruct> entities = static::getEntitiesFromModules(modules);
		entities = entities.prependCollectionIm( static::getEntities(c.env) );
		
		/* Base path */
		string base_path = (c.base_path != "") ? c.base_path : rtl::attr(c.env, ["BASE_PATH"], "", "string");
		
		/* Add entities */
		if (c.entities != null) entities = entities.appendCollectionIm(c.entities);
		c <= entities <= entities;
		
		/* Extend entities */
		entities = static::chain(c, "Runtime.Entities", [c, entities]);
		entities = static::extendEntities(c, entities);
		entities = static::extendEntitiesFromAnnotations(entities);
		
		/* Get providers */
		Dict<CoreStruct> providers = static::getProvidersFromEntities(entities);
		
		/* Register drivers */
		Dict<CoreDriver> drivers = static::getDriversFromEntities(entities);
		
		return c.copy
		{
			"modules": modules,
			"entities": entities,
			"providers": providers,
			"drivers": drivers,
			"base_path": base_path,
			"initialized": true,
		};
	}
	
	
	
	/**
	 * Start context
	 */
	static async Context start(Context c)
	{
		if (c.started) return c;
		Collection<string> drivers = c.drivers.keys();
		for (int i=0; i<drivers.count(); i++)
		{
			string driver_name = drivers.item(i);
			CoreDriver driver = c.drivers.item(driver_name);
			await driver.startDriver();
		}
		
		return c.copy{"started": true};
	}
	
	
	
	/* ---------------------- Driver -------------------- */
	
	/**
	 * Get driver
	 *
	 * @params string driver_name
	 * @return Runtime.anager
	 */
	pure CoreDriver getDriver(Context c, string driver_name)
	{
		if (c.drivers.has(driver_name))
		{
			return c.drivers.item(driver_name);
		}
		
		return null;
	}
	
	
	
	/* --------------------- Provider ------------------- */
	
	/**
	 * Create provider
	 *
	 * @params string provider_name
	 * @return CoreProvider
	 */
	pure CoreStruct createProvider(Context c, string provider_name, Dict params, string settings_name = "default")
	{
		CoreProvider provider = null;
		if (c.providers.has(provider_name))
		{
			Provider info = c.providers.item(provider_name);
			if (info.kind == "interface")
			{
				throw new RuntimeException("Provider " ~ provider_name ~ " does not declared");
			}
			string class_name = info.value;
			if (class_name == "") class_name = info.name;
			
			/* Set default params */
			if (params == null)
			{
				params = rtl::attr(c.settings, ["providers", class_name, settings_name]);
			}
			if (params == null) params = {};
			
			provider = rtl::newInstance(class_name, [params]);
			provider = static::chain(c, class_name, [provider]);
			if (provider_name != class_name)
			{
				provider = static::chain(c, provider_name, [provider]);
			}
		}
		else
		{
			throw new RuntimeException("Provider " ~ provider_name ~ " not found");
		}
		
		return provider;
	}
	
	
	
	/**
	 * Returns provider
	 *
	 * @params string provider_name
	 * @return CoreProvider
	 */
	pure CoreProvider getProvider(Context c, string provider_name, string settings_name = "default")
	{
		CoreProvider provider = static::createProvider(c, provider_name, null, settings_name);
		return provider;
	}
	
	
	
	/* ---------------------- Chain --------------------- */
	
	/**
	 * Apply Lambda Chain
	 */
	static var chain(Context c, string chain_name, Dict args)
	{
		Collection<CoreStruct> entities = c.entities.filter
		(
			bool (CoreStruct item) use (chain_name) => 
				item instanceof LambdaChain and 
				item.name == chain_name and 
				item.is_async == false
		);
		entities = entities.sortIm
		(
			int @(LambdaChain a, LambdaChain b) => a.pos > b.pos
		);
		
		for (int i=0; i<entities.count(); i++)
		{
			LambdaChain item = entities.item(i);
			string item_chain_name = item.chain;
			if (item_chain_name != "")
			{
				var res = c.chain(item_chain_name, args);
				args = args.setIm(args.count() - 1, res);
			}
			else
			{
				Collection arr = rs::split("::", item.value);
				string class_name = arr.get(0, "");
				string method_name = arr.get(1, "");
				fn f = rtl::method(class_name, method_name);
				var res = rtl::apply(f, args);
				args = args.setIm(args.count() - 1, res);
			}
		}
		var res = args.last();
		return res;
	}
	
	
	
	/**
	 * Apply Lambda Chain Await
	 */
	static async var chainAwait(Context c, string chain_name, Dict args)
	{
		Collection<CoreStruct> entities = c.entities.filter
		(
			bool (CoreStruct item) use (chain_name) => 
				item instanceof LambdaChain and 
				item.name == chain_name
		);
		entities = entities.sortIm
		(
			int @(LambdaChain a, LambdaChain b) => a.pos > b.pos
		);
		
		for (int i=0; i<entities.count(); i++)
		{
			LambdaChain item = entities.item(i);
			string item_chain_name = item.chain;
			if (item_chain_name != "")
			{
				var res = await static::chainAwait(item_chain_name, args);
				args = args.setIm(args.count() - 1, res);
			}
			else
			{
				Collection arr = rs::split("::", item.value);
				string class_name = arr.get(0, "");
				string method_name = arr.get(1, "");
				fn f = rtl::method(class_name, method_name);
				
				if (item.is_async)
				{
					var res = await rtl::apply(f, args);
					args = args.setIm(args.count() - 1, res);
				}
				else
				{
					var res = rtl::apply(f, args);
					args = args.setIm(args.count() - 1, res);
				}
			}
		}
		var res = args.last();
		return res;
	}
	
	
	
	/**
	 * Translate message
	 * @params string message - message need to be translated
	 * @params string space - message space
	 * @params Map params - Messages params. Default null.
	 * @params string locale - Different locale. Default "".
	 * @return string - translated string
	 */
	pure string translate(Context c, string message, string space, Map params = null, string locale = "")
	{
		return message;
	}
	
	
	
	/* ----------------------- Bus ---------------------- */
	
	/**
	 * Send message
	 * @param Message msg
	 * @param Context ctx
	 * @return Message
	 */ 
	static async Message sendMessage(Context c, Message msg)
	{
		BusInterface provider = static::getProvider(c, RuntimeConstant::BUS_INTERFACE, "default");
		msg = await provider->sendMessage(msg);
		return msg;
	}
	
	
	
	/* ---------------------- Logs ---------------------- */
	
	/**
	 * Log message
	 * @param string message
	 * @param int loglevel
	 */
	static void debug(Context c, string message, int loglevel = 0)
	{
		this.logs.push(message ~ "\n");
	}
	
	
	
	/**
	 * Timer message
	 * @param string message
	 * @param int loglevel
	 */
	static void log_timer(Context c, string message, int loglevel = 0)
	{
		int time = static::utime(c) - c.start_time;
		string s = "[" ~ rtl::round(time * 1000) ~ "]ms " ~ message ~ "\n";
		c.logs.push(s);
		#switch
		#case ifcode PHP then
		/*var_dump($s);*/
		#endswitch
	}
	
	
	
	/**
	 * Dump var to log
	 * @param var v
	 * @param int loglevel
	 */
	static void dump(Context c, var v, int loglevel = 0)
	{
		#switch
		#case ifcode PHP then
		ob_start();
		var_dump($v);
		$content = ob_get_contents();
		ob_end_clean();
		$this->logs->push($content);
		#endswitch
	}
	
	
	
	/**
	 * Append logs message
	 * @param Collection<string> logs
	 */
	static void logAppend(Context c, Collection<string> logs)
	{
		/*this.logs.appendVector(logs);*/
	}
	
	
	
	/**
	 * Return logs
	 */
	static Collection<string> getLogs(Context c)
	{
		/*return this.logs.toCollection();*/
		return [];
	}
	
	
	
	/* ---------------------- Tags ---------------------- */
	
	/**
	 * Set tag
	 */
	static Context setTagIm(Context c, string tag_name, var value)
	{
		return c.copy{ "tags": c.tags.setIm(c, tag_name, value) };
	}
	
	
	
	/**
	 * Returns tag
	 */
	static var getTag(Context c, string tag_name)
	{
		return c.tags.get(c, tag_name, null);
	}
	
	
	
	/* ---------------------- Other --------------------- */
	
	/**
	 * Returns unix timestamp
	 */
	static int time(Context c)
	{
		#switch
		#case ifcode PHP then
		return time();
		#endswitch
	}
	
	
	
	/**
	 * Returns unix timestamp
	 */
	static int utime(Context c)
	{
		#switch
		#case ifcode PHP then
		return microtime(true);
		#endswitch
	}
	
	
	/* -------------------- Functions ------------------- */
	
	/**
	 * Returns required modules
	 * @param string class_name
	 * @return Collection<string>
	 */
	pure void _getRequiredModules
	(
		Vector<string> res,
		Map<string> cache,
		Collection<string> modules,
		fn filter = null
	)
	{
		if (modules == null) return;
		if (filter) modules = modules.filter(filter);
		for (int i=0; i<modules.count(); i++)
		{
			string module_name = modules.item(i);
			if (cache.get(module_name, false) == false)
			{
				cache.set(module_name, true);
				fn f = rtl::method(module_name ~ ".ModuleDescription", "requiredModules");
				Dict<string> sub_modules = f();
				if (sub_modules != null)
				{
					Collection<string> sub_modules = sub_modules.keys();
					static::_getRequiredModules(res, cache, sub_modules);
				}
				res.push(module_name);
			}
		}
	}
	
	
	
	/**
	 * Returns all modules
	 * @param Collection<string> modules
	 * @return Collection<string>
	 */
	pure Collection<string> getRequiredModules(Collection<string> modules)
	{
		Vector<string> res = new Vector();
		Map<string> cache = new Map();
		static::_getRequiredModules(res, cache, modules);
		res = res.removeDublicatesIm();
		return res.toCollection();
	}
	
	
	
	/**
	 * Returns modules entities
	 */
	pure Collection<CoreStruct> getEntitiesFromModules(Collection<string> modules)
	{
		Vector<CoreStruct> entities = new Vector();
		for (int i=0; i<modules.count(); i++)
		{
			string module_class_name = modules.item(i) ~ ".ModuleDescription";
			fn f = rtl::method(module_class_name, "entities");
			Collection<CoreStruct> arr = f();
			entities.appendVector(arr);
		}
		return entities.toCollection();
	}
	
	
	
	/**
	 * Extend entities
	 */
	pure Collection<CoreStruct> extendEntitiesFromAnnotations(Collection<CoreStruct> entities)
	{
		Vector<CoreStruct> e = entities.toVector();
		for (int i=0; i<entities.count(); i++)
		{
			CoreStruct item1 = entities.item(i);
			string item1_class_name = item1.getClassName();
			if (item1_class_name == "Runtime.Annotations.Entity")
			{
				string class_name = (item1.value != "") ? item1.value : item1.name;
				IntrospectionClass info = RuntimeUtils::getClassIntrospection(class_name);
				if (info != null and info.class_info)
				{
					for (int j=0; j<info.class_info.count(); j++)
					{
						CoreStruct item2 = info.class_info.item(j);
						string item2_class_name = item2.getClassName();
						if (item2 instanceof Entity and item2_class_name != "Runtime.Annotations.Entity")
						{
							item2 = item2.copy({ "name": class_name });
							e.push(item2);
						}
					}
				}
			}	
		}
		return e.toCollection();
	}
	
	
	
	/**
	 * Returns providers from entities
	 */
	pure Dict<CoreStruct> getProvidersFromEntities(Collection<CoreStruct> entities)
	{
		Collection<CoreStruct> arr = entities.filter
		(
			bool (CoreStruct item) => item instanceof Provider
		);
		Map<CoreStruct> providers = new Map();
		for (int i=0; i<arr.count(); i++)
		{
			Provider item = arr.item(i);
			providers.set(item.name, item);
		}
		return providers.toDict();
	}
	
	
	
	/**
	 * Register drivers
	 */
	pure Dict<Driver> getDriversFromEntities(Collection<CoreStruct> entities)
	{
		Collection<CoreStruct> arr = entities.filter
		(
			bool (CoreStruct item) => item instanceof Driver
		);
		Map<CoreStruct> drivers = new Map();
		for (int i=0; i<arr.count(); i++)
		{
			Driver item = arr.item(i);
			
			string driver_name = item.name;
			string class_name = item.value;
			if (class_name == "") class_name = item.name;
			
			CoreDriver driver = rtl::newInstance(class_name, []);
			driver = static::chain(class_name, [driver]);
			if (class_name != driver_name)
			{
				driver = static::chain(driver_name, [driver]);
			}
			
			drivers.set(item.name, driver);
		}
		
		return drivers.toDict();
	}
	
	
	
	/**
	 * Extends entities
	 */
	pure Collection<CoreStruct> extendEntities(Context c, Collection<CoreStruct> entities) => entities;
	
}