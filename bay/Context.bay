/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2020 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime;

use Runtime.rs;
use Runtime.rtl;
use Runtime.lib;
use Runtime.BusResult;
use Runtime.Collection;
use Runtime.CoreDriver;
use Runtime.CoreObject;
use Runtime.CoreStruct;
use Runtime.CoreProvider;
use Runtime.Dict;
use Runtime.Map;
use Runtime.Message;
use Runtime.MessageRPC;
use Runtime.Vector;
use Runtime.RuntimeConstant;
use Runtime.RuntimeUtils;
use Runtime.Annotations.Driver;
use Runtime.Annotations.Entity;
use Runtime.Annotations.IntrospectionClass;
use Runtime.Annotations.IntrospectionInfo;
use Runtime.Annotations.LambdaChain;
use Runtime.Annotations.Provider;
use Runtime.Exceptions.RuntimeException;
use Runtime.Interfaces.BusInterface;
use Runtime.Interfaces.ModuleDescriptionInterface;


struct Context extends CoreStruct
{
	/* Struct data */
	string base_path = null;
	string entrypoint = "";
	Dict<string> enviroments = null;
	Dict<var> settings = null;
	Collection<string> modules = null;
	Collection<CoreStruct> entities = null;
	Collection<string> cli_args = null;
	Dict<CoreDriver> drivers = null;
	Dict<CoreStruct> providers = null;
	Dict<var> tags = null;
	
	/* Variable data */
	bool initialized = false;
	bool started = false;
	int start_time = 0;
	
	/* Private data */
	private Vector logs = new Vector();
	
	/* Main class */
	string main_class = "";
	
	
	/**
	 * Returns app name
	 * @return string
	 */
	pure string appName() => "";
	
	
	
	/**
	 * Returns context settings
	 * @return Dict<string>
	 */
	pure Dict<string> getSettings(Dict env) => null;
	
	
	
	/**
	 * Extends entities
	 */
	pure Collection<CoreStruct> getEntities(Collection<CoreStruct> entities) => null;
	
	
	
	/**
	 * Returns enviroment by eky
	 */
	pure fn env(string key, var def_value = "") => 
		string (Context c) use (key, def_value) =>
			c
				-> attr "enviroments"
				-> lib::get(key, def_value)
	;
	
	
	
	/**
	 * Returns settings
	 * @return Dict<string>
	 */
	pure fn config(Collection items, primitive d = null) =>
		var (Context c) use (items, d) =>
			c
				-> attr "settings"
				-> lib::get("config", null)
				-> lib::attr(items, d)
	;
	
	
	
	/**
	 * Returns docker secret key
	 */
	pure fn secret(string key) =>
		string (Context c) use (key) =>
			c
				-> attr "settings"
				-> lib::get(key, "")
	;
	
	
	
	/**
	 * Create context
	 *
	 * @params Dict env
	 * @params Collection<string> modules
	 * @params Dict settings
	 * @return Context
	 */
	static Context create(string main_module, Dict env = null)
	{
		string main_module_class_name = "";
		Dict settings = {};
		
		/* Get settings */
		if (main_module)
		{
			main_module_class_name = main_module ~ ".ModuleDescription";
			fn f = rtl::method(main_module_class_name, "appSettings");
			settings = f(env);
		}
		
		/* Context data */
		Dict obj = {
			"enviroments": env,
			"settings": settings,
			"main_class": main_module_class_name,
			"modules": [],
		};
		
		/* Add main module */
		if (main_module)
		{
			obj <= modules <= obj.item("modules").pushIm(main_module);
		}
		
		/* Create context */
		Context ctx = static::newInstance(obj);
		
		return ctx;
	}
	
	
	
	/**
	 * Init context
	 */
	static Context init(Context c)
	{
		if (c.initialized) return c;
		
		/* Extends modules */
		Collection<string> modules = static::getRequiredModules(c.modules);
		
		/* Get modules entities */
		Collection<CoreStruct> entities = static::getEntitiesFromModules(modules);
		entities = entities.prependCollectionIm( static::getEntities(c.env) );
		
		/* Base path */
		string base_path = (c.base_path != "") ? c.base_path : rtl::attr(c.env, ["BASE_PATH"], "", "string");
		
		/* Add entities */
		if (c.entities != null) entities = entities.appendCollectionIm(c.entities);
		c <= entities <= entities;
		
		/* Extend entities */
		entities = c -> method chain("Runtime.Entities", [c, entities]);
		entities = static::extendEntities(c, entities);
		entities = static::getRequiredEntities(entities);
		
		/* Get providers */
		Dict<CoreStruct> providers = static::getProvidersFromEntities(c, entities);
		
		/* Register drivers */
		Dict<CoreDriver> drivers = static::getDriversFromEntities(c, entities);
		
		return c.copy
		{
			"modules": modules,
			"entities": entities,
			"providers": providers,
			"drivers": drivers,
			"base_path": base_path,
			"initialized": true,
		};
	}
	
	
	
	/**
	 * Start context
	 */
	static async Context start(Context c)
	{
		if (c.started) return c;
		Collection<string> drivers = c.drivers.keys();
		for (int i=0; i<drivers.count(); i++)
		{
			string driver_name = drivers.item(i);
			CoreDriver driver = c.drivers.item(driver_name);
			await driver.startDriver();
		}
		
		return c.copy{"started": true};
	}
	
	
	
	/* ---------------------- Driver -------------------- */
	
	/**
	 * Get driver
	 *
	 * @params string driver_name
	 * @return Runtime.anager
	 */
	pure fn getDriver(string driver_name)
	{
		return CoreDriver(Context c) use (driver_name)
		{
			if (c.drivers.has(driver_name))
			{
				return c.drivers.item(driver_name);
			}
			
			return null;
		}
	}
	
	
	
	/* --------------------- Provider ------------------- */
	
	/**
	 * Create provider
	 *
	 * @params string provider_name
	 * @return CoreProvider
	 */
	pure fn createProvider(string provider_name, Dict params = null, string settings_name = "default")
	{
		return CoreStruct (Context c) use (provider_name, params, settings_name)
		{
			CoreProvider provider = null;
			if (c.providers.has(provider_name))
			{
				Provider info = c.providers.item(provider_name);
				if (info.kind == Provider::KIND_INTERFACE)
				{
					throw new RuntimeException("Provider " ~ provider_name ~ " does not declared");
				}
				string class_name = info.className();
				
				/* Set default params */
				if (params == null)
				{
					params = rtl::attr(c.settings, ["providers", class_name, settings_name]);
				}
				if (params == null) params = {};
				
				provider = rtl::newInstance(class_name, [params]);
				provider = c -> method chain(class_name, [provider]);
				if (provider_name != class_name)
				{
					provider = c -> method chain(provider_name, [provider]);
				}
			}
			else
			{
				throw new RuntimeException("Provider " ~ provider_name ~ " not found");
			}
			
			return provider;
		}
	}
	
	
	
	/**
	 * Returns provider
	 *
	 * @params string provider_name
	 * @return CoreProvider
	 */
	pure fn getProvider(string provider_name, string settings_name = "default") =>
		static::createProvider(provider_name, null, settings_name)
	;
	
	
	
	/* ---------------------- Chain --------------------- */
	
	/**
	 * Apply Lambda Chain
	 */
	static fn chain(string chain_name, Dict args)
	{
		return var (Context c) use (chain_name, args)
		{
			Collection<CoreStruct> entities = c.entities.filter
			(
				bool (CoreStruct item) use (chain_name) => 
					item instanceof LambdaChain and 
					item.name == chain_name and 
					item.is_async == false
			);
			entities = entities.sortIm
			(
				int (LambdaChain a, LambdaChain b) => a.pos > b.pos
			);
			
			for (int i=0; i<entities.count(); i++)
			{
				LambdaChain item = entities.item(i);
				string item_chain_name = item.chain;
				if (item_chain_name != "")
				{
					var res = c.chain(item_chain_name, args);
					args = args.setIm(args.count() - 1, res);
				}
				else
				{
					Collection arr = rs::split("::", item.value);
					string class_name = arr.get(0, "");
					string method_name = arr.get(1, "");
					fn f = rtl::method(class_name, method_name);
					var res = rtl::apply(f, args);
					args = args.setIm(args.count() - 1, res);
				}
			}
			var res = args.last();
			return res;
		}
	}
	
	
	
	/**
	 * Apply Lambda Chain Await
	 */
	static async fn chainAwait(string chain_name, Dict args)
	{
		return var (Context c) use (chain_name, args)
		{
			Collection<CoreStruct> entities = c.entities.filter
			(
				bool (CoreStruct item) use (chain_name) => 
					item instanceof LambdaChain and 
					item.name == chain_name
			);
			entities = entities.sortIm
			(
				int (LambdaChain a, LambdaChain b) => a.pos > b.pos
			);
			
			for (int i=0; i<entities.count(); i++)
			{
				LambdaChain item = entities.item(i);
				string item_chain_name = item.chain;
				if (item_chain_name != "")
				{
					var res = await static::chainAwait(item_chain_name, args);
					args = args.setIm(args.count() - 1, res);
				}
				else
				{
					Collection arr = rs::split("::", item.value);
					string class_name = arr.get(0, "");
					string method_name = arr.get(1, "");
					fn f = rtl::method(class_name, method_name);
					
					if (item.is_async)
					{
						var res = await rtl::apply(f, args);
						args = args.setIm(args.count() - 1, res);
					}
					else
					{
						var res = rtl::apply(f, args);
						args = args.setIm(args.count() - 1, res);
					}
				}
			}
			var res = args.last();
			return res;
		}
	}
	
	
	
	/**
	 * Translate message
	 * @params string space - message space
	 * @params string message - message need to be translated
	 * @params Map params - Messages params. Default null.
	 * @params string locale - Different locale. Default "".
	 * @return string - translated string
	 */
	pure fn translate(Context c, string space, string message, Map params = null, string locale = "")
	{
		message = (params == null) ? message :
			params.reduce
			(
				string (string message, string value, string key) =>
					rs::replace("%" ~ key ~ "%", value, message)
				,
				message
			)
		;
		return message;
	}
	
	
	
	/* ----------------------- Bus ---------------------- */
	
	
	/**
	 * Send message
	 * @param Message msg
	 * @return Message
	 */ 
	static fn send(MessageRPC msg) =>
		async MessageRPC (Context c) use (msg) =>
			c
				-> method getProvider(RuntimeConstant::BUS_INTERFACE, "default")
				-> await method sendMessage( msg )
				-> monad MessageRPC::end
			;
			
	
	/**
	 * Send rpc message
	 * @param Dict items
	 * @return Message
	 */ 
	static fn sendMessage(Dict items) =>
		async MessageRPC (Context c) use (items) =>
			c
				-> method getProvider(RuntimeConstant::BUS_INTERFACE, "default")
				-> await method sendMessage( MessageRPC::create(items) )
				-> monad MessageRPC::end
			;
	
	
	/* ---------------------- Logs ---------------------- */
	
	/**
	 * Log message
	 * @param string message
	 * @param int loglevel
	 */
	static fn debug(string message, int loglevel = 0)
	{
		return Message (Context c) use (message, loglevel)
		{
			this.logs.push(message ~ "\n");
		}
	}
	
	
	
	/**
	 * Timer message
	 * @param string message
	 * @param int loglevel
	 */
	static fn log_timer(string message, int loglevel = 0)
	{
		return void (Context c) use (message, loglevel)
		{
			int time = c -> method utime;
			time = time - c.start_time;
			string s = "[" ~ rtl::round(time * 1000) ~ "]ms " ~ message ~ "\n";
			c.logs.push(s);
			#switch
			#case ifcode PHP then
			/*if (isset($_GET['aaa']) && $_GET['aaa'] == 'bbb') var_dump($s);*/
			#endswitch
		}
	}
	
	
	
	/**
	 * Dump var to log
	 * @param var v
	 * @param int loglevel
	 */
	static fn dump(var v, int loglevel = 0)
	{
		return void (Context c) use (v, loglevel)
		{
			#switch
			#case ifcode PHP then
			ob_start();
			var_dump($v);
			$content = ob_get_contents();
			ob_end_clean();
			$this->logs->push($content);
			#endswitch
		}
	}
	
	
	
	/**
	 * Append logs message
	 * @param Collection<string> logs
	 */
	static fn logAppend(Collection<string> logs)
	{
		return void (Context c) use (logs)
		{
			/*this.logs.appendVector(logs);*/
		}
	}
	
	
	
	/**
	 * Return logs
	 */
	static fn getLogs()
	{
		return Collection<string> (Context c)
		{
			/*return this.logs.toCollection();*/
			return [];
		}
	}
	
	
	
	/* ---------------------- Tags ---------------------- */
	
	/**
	 * Set tag
	 */
	static fn setTagIm(string tag_name, var value)
	{
		return Context(Context c) use (tag_name, value)
		{
			return c.copy{ "tags": c.tags.setIm(c, tag_name, value) };
		}
	}
	
	
	
	/**
	 * Returns tag
	 */
	static fn getTag(string tag_name)
	{
		return Context(Context c) use (tag_name)
		{
			return c.tags.get(c, tag_name, null);
		}
	}
	
	
	
	/* ---------------------- Other --------------------- */
	
	/**
	 * Returns unix timestamp
	 */
	static int time()
	{
		#switch
		#case ifcode PHP then
		return time();
		#endswitch
	}
	
	
	
	/**
	 * Returns unix timestamp
	 */
	static int utime()
	{
		#switch
		#case ifcode PHP then
		return microtime(true);
		#endswitch
	}
	
	
	/* -------------------- Functions ------------------- */
	
	/**
	 * Returns required modules
	 * @param string class_name
	 * @return Collection<string>
	 */
	pure void _getRequiredModules
	(
		Vector<string> res,
		Map<string> cache,
		Collection<string> modules,
		fn filter = null
	)
	{
		if (modules == null) return;
		if (filter) modules = modules.filter(filter);
		for (int i=0; i<modules.count(); i++)
		{
			string module_name = modules.item(i);
			if (cache.get(module_name, false) == false)
			{
				cache.set(module_name, true);
				fn f = rtl::method(module_name ~ ".ModuleDescription", "requiredModules");
				Dict<string> sub_modules = f();
				if (sub_modules != null)
				{
					Collection<string> sub_modules = sub_modules.keys();
					static::_getRequiredModules(res, cache, sub_modules);
				}
				res.push(module_name);
			}
		}
	}
	
	
	
	/**
	 * Returns all modules
	 * @param Collection<string> modules
	 * @return Collection<string>
	 */
	pure Collection<string> getRequiredModules(Collection<string> modules)
	{
		Vector<string> res = new Vector();
		Map<string> cache = new Map();
		static::_getRequiredModules(res, cache, modules);
		res = res.removeDublicatesIm();
		return res.toCollection();
	}
	
	
	
	/**
	 * Returns modules entities
	 */
	pure Collection<CoreStruct> getEntitiesFromModules(Collection<string> modules)
	{
		Vector<CoreStruct> entities = new Vector();
		for (int i=0; i<modules.count(); i++)
		{
			string module_class_name = modules.item(i) ~ ".ModuleDescription";
			fn f = rtl::method(module_class_name, "entities");
			Collection<CoreStruct> arr = f();
			entities.appendVector(arr);
		}
		return entities.toCollection();
	}
	
	
	
	/**
	 * Extend entities
	 */
	pure Collection<CoreStruct> getRequiredEntities(Collection<CoreStruct> entities)
	{
		Vector<CoreStruct> e = entities.toVector();
		for (int i=0; i<entities.count(); i++)
		{
			CoreStruct item1 = entities.item(i);
			string item1_class_name = item1.getClassName();
			if (item1_class_name == "Runtime.Annotations.Entity")
			{
				string class_name = (item1.value != "") ? item1.value : item1.name;
				IntrospectionClass info = RuntimeUtils::getClassIntrospection(class_name);
				if (info != null and info.class_info)
				{
					for (int j=0; j<info.class_info.count(); j++)
					{
						CoreStruct item2 = info.class_info.item(j);
						string item2_class_name = item2.getClassName();
						if (item2 instanceof Entity and item2_class_name != "Runtime.Annotations.Entity")
						{
							item2 = item2.copy({ "name": class_name });
							e.push(item2);
						}
					}
				}
			}	
		}
		return e.toCollection();
	}
	
	
	
	/**
	 * Returns providers from entities
	 */
	pure Dict<CoreStruct> getProvidersFromEntities(Context c, Collection<CoreStruct> entities)
	{
		Collection<CoreStruct> arr = entities.filter
		(
			bool (CoreStruct item) => item instanceof Provider
		);
		Map<CoreStruct> providers = new Map();
		for (int i=0; i<arr.count(); i++)
		{
			Provider item = arr.item(i);
			providers.set(item.name, item);
		}
		return providers.toDict();
	}
	
	
	
	/**
	 * Register drivers
	 */
	pure Dict<Driver> getDriversFromEntities(Context c, Collection<CoreStruct> entities)
	{
		Collection<CoreStruct> arr = entities.filter
		(
			bool (CoreStruct item) => item instanceof Driver
		);
		Map<CoreStruct> drivers = new Map();
		for (int i=0; i<arr.count(); i++)
		{
			Driver item = arr.item(i);
			
			string driver_name = item.name;
			string class_name = item.value;
			if (class_name == "") class_name = item.name;
			
			CoreDriver driver = rtl::newInstance(class_name, []);
			driver = c -> method chain(class_name, [driver]);
			if (class_name != driver_name)
			{
				driver = c -> method chain(driver_name, [driver]);
			}
			
			drivers.set(item.name, driver);
		}
		
		return drivers.toDict();
	}
	
	
	
	/**
	 * Extends entities
	 */
	pure Collection<CoreStruct> extendEntities(Context c, Collection<CoreStruct> entities) => entities;
	
}