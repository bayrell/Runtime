/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2018 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.bayrell.org/licenses/APACHE-LICENSE-2.0.html
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime;

use Runtime.rtl;
use Runtime.CoreObject;
use Runtime.Map;
use Runtime.Vector;
use Runtime.Interfaces.ContextInterface;
use Runtime.Interfaces.FactoryInterface;
use Runtime.Interfaces.ModuleDescriptionInterface;


class Context extends CoreObject implements ContextInterface{
	
	
	protected string _locale = "";
	protected Vector<string> _modules = null;
	protected Map<string, mixed> _values = null;
	protected Map<string, FactoryInterface> _providers_names = null;
	
	
	/**
	 * Constructor
	 */
	void constructor(){
		parent();
		this._modules = new Vector<string>();
		this._values = new Map<string, mixed>();
		this._providers_names = new Map<string, FactoryInterface>();
	}
	
	
	
	/**
	 * Destructor
	 */
	public void destructor(){
		delete this._providers_classes_names;
		delete this._modules;
		parent();
	}
	
	
	
	/**
	 * Returns registed modules
	 * @return Vector<string>
	 */
	Vector<string> getModules(){
		return this._modules.copy();
	}
	
	
	
	/**
	 * Register module
	 */
	ContextInterface registerModule(string module_name){
		
		string module_description_class_name = module_name ~ ".ModuleDescription";
		if (this._modules.indexOf(module_description_class_name) != -1)
			return;
		
		/* Add module */
		this._modules.push(module_description_class_name);
		
		/* Call onRegister */
		Vector<var> args = (new Vector()).push(this);
		rtl::callStaticMethod(
			module_description_class_name, 
			"onRegister",
			args
		);
		
		/* Register required Modules*/
		Map<string, string> modules = rtl::callStaticMethod(
			module_description_class_name, 
			"getRequiredModules",
			args
		);
		if (modules != null){
			Vector<string> keys = modules.keys();
			int sz = keys.count();
			for (int i=0; i<sz; i++){
				string module_name = keys.item(i);
				this.registerModule(module_name);
			}
			delete keys;
			delete modules;
		}
		delete args;
		
		return this;
	}
	
	
	
	/**
	 * Register module
	 * @param string provider_name
	 * @param string factory_name
	 * @param int type
	 */
	ContextInterface registerProvider(string provider_name, FactoryInterface factory){
		if (not this._providers_names.has(provider_name)){
			this._providers_names.set(provider_name, factory);
		}
		return this;
	}
	
	
	
	/**
	 * Init context
	 */
	void init(){
		Vector<var> args = new Vector();
		args.push(this);	
	
		int sz = this._modules.count();
		for (int i=0; i<sz; i++){
			string module_description_class_name = this._modules.item(i);
			rtl::callStaticMethod(
				module_description_class_name, 
				"initContext",
				args
			);
		}
		
		delete args;
	}
	
	
	
	/**
	 * Returns provider description by name. If provider does not exists returns null.
	 * @param string provider_name
	 * @return ProviderDescription 
	 */
	public ProviderDescription getProviderDescription(string provider_name){
		if (not this._providers_names.has(provider_name))
			return null;
		
		return this._providers_names.item(provider_name);
	}
	
	
	
	/**
	 * Returns temporary provider
	 *
	 * @params string provider_name
	 * @return CoreObject
	 */
	public CoreObject createProvider(string provider_name){
		if (!this._providers_names.has(provider_name))
			return null;
			
		FactoryInterface factory_obj = this._providers_names.item(provider_name);
		if (factory_obj == null)
			return null;
		
		CoreObject obj = factory_obj.newInstance(this);
		return obj;
	}

	
	
	/**
	 * Set value
	 * @param string name
	 * @param mixed value
	 */
	public mixed setValue(string name, mixed value){
		this._values.set(name, value);
	}
	


	/**
	 * Get value
	 * @param string name
	 * @param mixed value
	 */
	public mixed getValue(string name, mixed default_value = null){
		return this._values.get(name, default_value);
	}

	
	
	/**
	 * Set application locale
	 * @params string locale
	 */
	void setLocale(string locale){
		this._locale = locale;
	}
	
	
	
	/**
	 * Get application locale
	 * @params string locale
	 */
	string getLocale(){
		return this._locale;
	}
	
	
	
	/**
	 * Translate message
	 * @params string message - message need to be translated
	 * @params Map params - Messages params. Default null.
	 * @params string locale - Different locale. Default "".
	 * @return string - translated string
	 */
	string translate(string message, Map params = null, string locale = ""){
		return message;
	}
	
}