/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2018 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.bayrell.org/licenses/APACHE-LICENSE-2.0.html
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */
 
namespace Runtime;

use Runtime.rtl;
use Runtime.AsyncTask;
use Runtime.CoreObject;
use Runtime.Vector;


class AsyncThread extends CoreObject{
	
	
	protected callback f = null;
	protected string pos = "0";
	protected mixed res = null;
	protected mixed err = null;
	protected Vector<AsyncTask> run_stack = null;
	protected Vector<string> catch_stack = null;
	
	
	/**
	 * Constructor
	 */
	void constructor(){
		super();
		this.reset();
	}
	
	
	
	/**
	 * Reset AsyncThread
	 */
	void reset(){
		this.pos = "0";
		this.res = null;
		this.err = null;
		this.run_stack = new Vector<AsyncTask>();
		this.catch_stack = new Vector<string>();
	}
	
	
	
	/**
	 * Returns current position
	 * @return string
	 */
	string current(){
		return this.pos;
	}
	
	
	
	/**
	 * Returns result of the prev function
	 * @return mixed
	 */
	mixed result(){
		return this.res;
	}
	
	
	
	/**
	 * Returns error of the prev function
	 * @return mixed
	 */
	mixed getError(){
		return this.err;
	}

	
	
	/**
	 * Clear error
	 */
	void clearError(){
		this.err = null;
	}
	
	
	
	/**
	 * Resolve thread with result
	 * @param mixed res
	 */
	string resolve(mixed res){
		this.res = res;
		this.next();
		this.forward();
		return "resolve";
	}
	
	
	
	/**
	 * Set position
	 * @param string pos
	 */
	string jump(string pos){
		this.pos = pos;
		return "jump";
	}
	
	
	
	/**
	 * Resolve Exception
	 * @param mixed res
	 */
	string error(mixed err){
		if (this.catch_stack.count() == 0){
			this.err = err;
			this.next(true);
		}
		else{
			this.err = err;
			this.pos = this.catch_stack.pop();
		}
		this.forward();
		return "error";
	}
	
	
	
	/**
	 * Push catch
	 * @param string pos
	 */
	void catchPush(string pos){
		this.catch_stack.push(pos);
	}
	
	
	
	/**
	 * Pop catch
	 */
	void catchPop(){
		this.catch_stack.pop();
	}
	
	
	
	/**
	 * Call next
	 */
	string next(bool is_error = false){
		
		if (this.run_stack.count() == 0){
			this.pos = "-1";
			return;
		}
		
		AsyncTask task = this.run_stack.pop();
		
		/* Restore pos */
		this.f = task.f;
		this.pos = task.pos;
		this.catch_stack = task.catch_stack;
		
		if (is_error){
			if (this.catch_stack.count() == 0){
				this.pos = "-1";
			}
			else{
				this.pos = this.catch_stack.pop();
			}
		}
		
		return "next";
	}
	
	
	
	/**
	 * Forward call
	 */
	string forward(){
		
		bool is_browser = rtl::isBrowser();
		
		#switch
		#case ifcode JAVASCRIPT then
		var _rtl; if (is_browser) _rtl = Runtime.rtl; else _rtl = rtl;
		var _AsyncTask; if (is_browser) _AsyncTask = Runtime.AsyncTask; else _AsyncTask = AsyncTask;
		var _Vector; if (is_browser) _Vector = Runtime.Vector; else _Vector = Vector;
		setTimeout( 
			()=>{
				if (this.pos == "-1"){
					if (this.err != null){
						throw this.err;
					}
					return;
				}
				
				/* Call task function */
				var res = null;
				try{
					res = _rtl.call(this.f, [this]);
				}
				catch (e){
					res = null;
					this.error(e);
				}
				
				/* If can forward */
				if (res != null){
				
					/* If return next async function */
					if (res instanceof Function){
						var task = new _AsyncTask();
						task.f = this.f;
						task.pos = this.pos;
						task.catch_stack = this.catch_stack;
						this.run_stack.push(task);
						this.catch_stack = new _Vector();
						this.pos = "0";
						this.f = res;
					}
					
					/* Call forward */
					if (res != "error" && res != "resolve"){
						this.forward();
					}
				}
			}, 1);
		#endswitch
		
		return "forward";
	}
	
	
	
	/**
	 * Run async await
	 */
	void run(callback f){
		this.pos = "0";
		this.f = f;
		this.forward();
	}
	
	
	
	/**
	 * Call if thread is ended
	 */
	string end(){
		this.pos = "-1";
		return "end";
	}
}